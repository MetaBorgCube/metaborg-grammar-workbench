module lex

imports

  Common

context-free start-symbols

  Grammar

template options

  keyword -/- [0-9A-Za-z]
  ID = keyword {reject}
  tokenize : "({;"

context-free syntax

  // TODO named character classes before first %%
  // TODO glob all code after last %%
  Grammar.Grammar = <
                     <{Def "\n"}*>
                     %%
                     <{Rule "\n"}*><Tail?>
                    >

  // TODO start conditions, character tables, changes to internal array sizes,
  Def.Name = <<ID> <RegexElement*>>
  Def.Code     = <
                  %{
                      <CodeAnything?>
                  %}
                 >

  Rule.RuleWithRHS = <<RegexElement*> { <Statement*>return <ID>; }> {prefer}
  Rule.RuleWithError = <<RegexElement*> { <Statement*>yyerror(<STRING>); }> {prefer}
  Rule.RuleWithLayout = <<RegexElement*> { <Statement*>}>

  // TODO repetitions with {\d,\d}
  RegexElement.Any = "."
  RegexElement.Lit = STRING
  RegexElement.Ref = <{<ID>}>
  RegexElement.Or = <<RegexElement> | <RegexElement>> {right}
  RegexElement.Op = <<RegexElement><RegOperator>>
  RegexElement.Sequence = <(<RegexElement> <RegexElement+>)>
  RegexElement = <(<RegexElement>)> {bracket}

  Statement.Statement = <<StatementChars>; >

  Tail.Tail = <

               %%
               <TailAnything>
              >

context-free priorities

  {RegexElement.CharClassNotHyphen RegexElement.CharClassNotBracket} >
  {RegexElement.CharClassNot RegexElement.CharClassNotHyphen RegexElement.CharClassBracket} >
  {RegexElement.CharClass}

syntax

  // Note that character classes may not be empty, enforced with + in the non-Hyphen and non-Bracket cases
  // TODO if there is no RegOperator, []] or [^]] gives ambiguities
  RegexElement-CF.CharClass = "[" {CharClassElem ""}+ Hyphen?-CF "]" {avoid}
  RegexElement-CF.CharClassHyphen = "[-" {CharClassElem ""}* "]"
  RegexElement-CF.CharClassBracket = "[]" {CharClassElem ""}* Hyphen?-CF "]"
  RegexElement-CF.CharClassNot = "[^" {CharClassElem ""}+ Hyphen?-CF "]"
  RegexElement-CF.CharClassNotHyphen = "[^-" {CharClassElem ""}* "]" {prefer}
  RegexElement-CF.CharClassNotBracket = "[^]" {CharClassElem ""}* Hyphen?-CF "]" {prefer}

  CharClassElem = CharClassElemShort-CF
  CharClassElem = CharClassElemRange-CF
  CharClassElemShort-CF.Short = CharClassChar-CF
  CharClassElemRange-CF.Range = CharClassElemShort-CF "-" CharClassElemShort-CF

lexical syntax

  CodeAnything = InsideCodeAnything*
  InsideCodeAnything = ~[\%]
  InsideCodeAnything = CodePercentChar
  CodePercentChar = "%"

  Hyphen = "-"
  RegOperator = [\*\+\?]

  StatementChars = ~[\{\}\;]*
  CharClassChar = ([\32-\126] / [\]\-\\]) | ("\\" [\\nrt])

  TailAnything = (~[])*
