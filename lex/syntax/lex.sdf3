module lex

imports

  Common

context-free start-symbols

  Grammar

template options

  keyword -/- [0-9A-Za-z]
  ID = keyword {reject}
  tokenize : "({;"

context-free syntax

  // TODO named character classes before first %%
  // TODO glob all code after last %%
  Grammar.Grammar = <
                     <{Def "\n"}*>
                     %%
                     <{Rule "\n"}*><Tail?>
                    >

  // TODO start conditions, character tables, changes to internal array sizes,
  Def.Name = <<ID> <Regex>>
  Def.Code     = <
                  %{
                      <CodeAnything?>
                  %}
                 >

  Rule.RuleWithRHS = <<Regex> { <Statement*>return <ID>; }> {prefer}
  Rule.RuleWithError = <<Regex> { <Statement*>yyerror(<STRING>); }> {prefer}
  Rule.RuleWithLayout = <<Regex> { <Statement*>}>

  // TODO repetitions with {\d,\d}; literal alphanum or escaped characters
  Regex.Any = "."
  Regex.Lit = STRING
  Regex.Ref = <{<ID>}>
  Regex.Or = <<Regex> | <Regex>> {right}
  Regex.Conc = <<Regex><Regex>> {right}
  Regex.Op = <<Regex><RegOperator>>
  Regex = <(<Regex>)> {bracket}

  Statement.Statement = <<StatementChars>; >

  Tail.Tail = <

               %%
               <TailAnything>
              >

context-free priorities

  {Regex.Op} >
  {Regex.CharClassNotHyphen Regex.CharClassNotBracket} >
  {Regex.CharClassNot Regex.CharClassNotHyphen Regex.CharClassBracket} >
  {Regex.CharClass} >
  {Regex.Conc} >
  {Regex.Or}

syntax

  // Note that character classes may not be empty, enforced with + in the non-Hyphen and non-Bracket cases
  // TODO if there is no RegOperator, []] or [^]] gives ambiguities
  Regex-CF.CharClass = "[" {CharClassElem ""}+ Hyphen?-CF "]" {avoid}
  Regex-CF.CharClassHyphen = "[-" {CharClassElem ""}* "]"
  Regex-CF.CharClassBracket = "[]" {CharClassElem ""}* Hyphen?-CF "]"
  Regex-CF.CharClassNot = "[^" {CharClassElem ""}+ Hyphen?-CF "]"
  Regex-CF.CharClassNotHyphen = "[^-" {CharClassElem ""}* "]" {prefer}
  Regex-CF.CharClassNotBracket = "[^]" {CharClassElem ""}* Hyphen?-CF "]" {prefer}

  CharClassElem = CharClassElemShort-CF
  CharClassElem = CharClassElemRange-CF
  CharClassElemShort-CF.Short = CharClassChar-CF
  CharClassElemRange-CF.Range = CharClassElemShort-CF "-" CharClassElemShort-CF

lexical syntax

  CodeAnything = InsideCodeAnything*
  InsideCodeAnything = ~[\%]
  InsideCodeAnything = CodePercentChar
  CodePercentChar = "%"

  Hyphen = "-"
  RegOperator = [\*\+\?]

  StatementChars = ~[\{\}\;]*
  CharClassChar = ([\32-\126] / [\]\-\\]) | ("\\" [\\nrt])

  TailAnything = (~[])*
