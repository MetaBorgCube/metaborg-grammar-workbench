module codegen/build

imports

  libspoofax/resource/path
  codegen/-
  trans/pp

rules

  /**
   * Transform YACC AST to SDF3 AST and show the resulting ATerm.
   *
   * @type YACC AST -> SDF3 AST
   */
  show-sdf3:
    (selected, _, ast, path, project-path) -> (filename, result)
    with
      filename := <guarantee-extension(|$[sdf3.aterm])> path
    ; result   := <yacc-to-sdf3> ast

  /**
   * Generate SDF3 files (.sdf3 files) for the YACC AST.
   *
   * @type YACC AST -> None
   */
  // TODO Automagically open file after generation
  generate-sdf3:
    (selected, _, ast, path, project-path) -> None()
    with
      name := <remove-extension> path
    ; <yacc-to-sdf3; write-sdf3(|name)> ast

rules

  /**
   * Writes an SDF3 file to the file system.
   *
   * @type String -> SDF3 AST -> Filename
   */
  write-sdf3(|name):
    f -> fn
    where
      fn  := $[[name].sdf3]
    ; <fputs; fclose> (<pp-SDF3-string> f, <fopen> (fn, "w"))
