module codegen/codegen-yacc

imports

  nabl2/api
  libspoofax/stratego/debug
  signatures/TemplateLang-sig

  signatures/yacc-sig
  codegen/-

rules

  /** @type YACC AST -> SDF3 ContextFreeSyntax */
  yacc-to-sdf3: g @ Grammar(definitions, productionRules, _) -> Module(
      Unparameterized("test")
    , [Imports([Module(Unparameterized("test-lex"))])]
    , <concat> [
        [ SDFSection(ContextFreeStartSymbols([<yacc-get-start-symbol> g])) ]
      , <yacc-get-priorities> g
        // TODO take into account the definitions
      , [ SDFSection(ContextFreeSyntax(<mapconcat(rule-to-sdf3)> productionRules)) ]
      ]
    )

  /**
   * Gets the (first, and only) start symbol in the definitions list.
   * If there is no start symbol, it uses the lhs of the first grammar rule as start symbol.
   * When there are no rules, this strategy returns Sort("Undefined"),
   *   but this should never happen as NABL2 enforces the file to have at least one rule.
   *
   * @type YACC AST -> sdf3.Sort
   */
  yacc-get-start-symbol: Grammar(_, [], _) -> Sort("Undefined")
  yacc-get-start-symbol: Grammar(definitions, [Rule(firstSymbol, _) | _], _) ->
      <getfirst(\ Start(symbol) -> Sort(<capitalize-string> symbol) \)
          <+ !Sort(<capitalize-string> firstSymbol)> definitions

  /**
   * Optinally returns an SDF section with context-free priorities.
   * The priorities are in reverse order with respect to the definition in YACC, to maintain semantics.
   *
   * @type YACC AST -> List(sdf3.SDFSection){length <= 1}
   */
  yacc-get-priorities: Grammar(definitions, productionRules, _) -> result
    with filteredDefs := <reverse> <filter(?Left(_, _) <+ ?Right(_, _) <+ ?NonAssoc(_,_))> definitions
       ; groups := <map(assoc-rule-to-sdf3(|productionRules))> filteredDefs
       ; result := <\[] -> []\ <+ \ groups -> [ SDFSection(ContextFreePriorities([ Chain(groups) ])) ] \> groups

  /** @type Left | Right | NonAssoc -> sdf3.AssocRefGroup */
  assoc-rule-to-sdf3(|productionRules): associativity#([_, ids]) -> AssocRefGroup(
      <assoc-to-sdf3> associativity
    , <mapconcat(lookup-constructor-from-identifier(|productionRules))> ids
    )

  /**
   * Returns the SDF3 associativity constructor based on the constructor symbol from YACC
   * @type String -> sdf3.Left | sdf3.Right | sdf3.NonAssoc
   */
  assoc-to-sdf3: "Left" -> Left()
  assoc-to-sdf3: "Right" -> Right()
  assoc-to-sdf3: "NonAssoc" -> NonAssoc()

  /**
   * Associativity rules can defined by their tokens, e.g. `%left '+'`.
   * In that case, this strategy looks up all constructors that contain this token on their RHS.
   * When a rule is defined with an identifier, e.g. `%left UMINUS`,
   *   the constructors that are annotated with `%prec UMINUS` are looked up.
   *
   * @type Identifier -> List(sdf3.SortConsRef(Sort, Constructor)))
   */
  // TODO for IDs, look up the constructors with corresponding %prec rule(s)
  // TODO for Strings, look up the constructors containing this token on their RHS.
  lookup-constructor-from-identifier(|productionRules): ID(name) -> 
      [ SortConsRef(Sort("Expr"), Constructor("Expr2")) ]
  lookup-constructor-from-identifier(|productionRules): String(str) ->
      [ SortConsRef(Sort("Expr"), Constructor("Expr2")) ]
  lookup-constructor-from-identifier(|rs): SQString(str) -> <lookup-constructor-from-identifier(|rs)> String(str)

  /** @type Rule(ID, List(RHS)) -> List(SdfProductionWithCons) */
  rule-to-sdf3: Rule(lhs, rhss) ->
      <map-with-index(rhs-to-sdf3(|lhs, <length> filteredRhss))> filteredRhss
      // Removes all rules with `error` token in RHS. TODO do we really want to ignore the error?
      with filteredRhss := <remove-all(\ RHS(rhsSymbols, _, _) -> <getfirst(?Error())> rhsSymbols \)> rhss

  /** @type (String, Int) -> (Int, RHS(List(Identifier), Prec, Action)) -> SdfProductionWithCons */
  rhs-to-sdf3(|lhs, rhsAmount): (index, RHS(rhsSymbols, prec, _)) -> SdfProductionWithCons(
        SortCons(
          SortDef(<capitalize-string> lhs)
        , Constructor(<conc-strings> (
            <capitalize-string> lhs
            // Add an index to the constructor only if the amount of rules > 1
          , <?1 < !"" + (!index ; int-to-string)> rhsAmount)
          )
        )
      , Rhs(<rhs-symbols-to-sdf3> rhsSymbols)
      , NoAttrs()
      )

  /**
   * If RHS is empty, it produces an empty string on the RHS.
   * Else, it maps the identifiers on the RHS to sdf3 sorts and literals.
   *
   * @type List(Identifier) -> List(sdf3.Sort | sdf3.Lit)
   */
  rhs-symbols-to-sdf3: [] -> [Lit("\"\"")]
  rhs-symbols-to-sdf3: rhsSymbols -> <map(identifier-to-sdf3)> rhsSymbols

  /** @type Identifier -> sdf3.Sort | sdf3.Lit */
  identifier-to-sdf3: ID(str) -> Sort(<capitalize-string> str)
  identifier-to-sdf3: String(str) -> Lit(str)
  identifier-to-sdf3: SQString(str) -> Lit(<double-quote> <un-single-quote> str)

  /**
   * Capitalizes the first string of the character
   *
   * @type String -> String
   * @fails Never
   */
  capitalize-string: string -> res
    with [h|tail] := <explode-string> string
       ; newH := <to-upper> h
       ; res := <implode-string> [newH|tail]
